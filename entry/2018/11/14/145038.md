
+++
date = "2018-11-14 14:50:38 +0000 UTC"
draft = false
title = "GoとKotlinのマルチプロジェクトをBazelでビルドする"
tags = ["Bazel","Go","Kotlin","Multiprojects"]

+++
Googleが開発するビルドツールのBazelを試していく。

<a href="https://bazel.build/">Bazel - a fast, scalable, multi-language and extensible build system" - Bazel</a>

### モチベーション

GoやKotlinでつくるマイクロサービス開発は１つのサービスに１つのレポジトリで行ってきた経緯がある。またCIではDockerfileをレポジトリに内包することでビルド時にDockerイメージの錬成を行いRegistryへのPushを行う。GoやKotlinのビルド環境が異なることやイメージのビルドがDockerfileドリブンであるためサービス対レポジトリが1:1の関係であった。

Bazelは複数言語のビルドをサポートする。またDockerfileを使わずにイメージをビルドができる。

１つのレポジトリにGoとKotlinのマイクロサービスを構造化したプロジェクトのビルドをBazelで実現できるのか？を検証することがエントリのモチベーションである。

### ゴール

簡易的なHello Worldを出力するアプリケーションをGoとKotlinでつくる。また言語ごとにアプリケーションから参照するライブラリを作りマルチプロジェクトの構成も取り入れる。そしてBazelでアプリケーションをDockerイメージにビルドするまでをゴールと定義する。

### プロジェクト構成

<code>pkg</code>以下にアプリケーションを配置する構成を定義する。

```
./
├── README.md
└── pkg
    ├── common_go
    │   └── util
    │       └── string.go
    ├── common_kt
    │   └── util
    │       └── string.kt
    ├── public_go
    │   └── main.go
    └── public_kt
        └── main.kt
```


<code>public_go</code>と<code>public_kt</code>はHello Worldを出力するアプリケーションを配置する。<code>common_go</code>と<code>common_kt</code>は各言語が参照するライブラリを配置する。

### Kotlin

まずはKotlinからビルドしていく。Bazelのインストールは調べるとキャッチアップできるので割愛する。

Bazelは<code>WORKSPACE</code>というファイルをプロジェクトの最上位に配置する。そして<code>BUILD</code>というファイルを各アプリケーションに配置することでビルドを行う。

#### WORKSPACE

WORKSPACEはビルド全般の設定を定義するものと理解すると分かりやすい。

```python
# ./WORKSPACE

load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

rules_kotlin_version = "87bd13f91d166a8070e9bbfbb0861f6f76435e7a"

http_archive(
    name = "io_bazel_rules_kotlin",
    urls = ["https://github.com/bazelbuild/rules_kotlin/archive/%s.zip" % rules_kotlin_version],
    type = "zip",
    strip_prefix = "rules_kotlin-%s" % rules_kotlin_version
)

load("@io_bazel_rules_kotlin//kotlin:kotlin.bzl", "kotlin_repositories", "kt_register_toolchains")

kotlin_repositories()

kt_register_toolchains()

```


<code>pkg</code>と同じ階層に上記のWORKSPACEファイルを配置する。Bazelはrulesという各言語ごとのビルド定義を用意している。Kotlinは次のレポジトリから参照できる。


<div class="github-card" data-user="bazelbuild" data-repo="rules_kotlin" data-width="400" data-height="" data-theme="default"></div>
<script src="https://cdn.jsdelivr.net/github-cards/latest/widget.js"></script>


WORKSPACEで行っていることはbazel_toolsから<code>http_archive</code>を読み込み<code>io_bazel_rules_kotlin</code>の名前でrulesをインストールしている。そして<code>kotlin_repositories</code>と<code>kt_register_toolchains</code>を有効にしている。

Bazelの良いところはWORKSPACEに定義したビルド設定を各自が実行すればビルド環境を簡単に再現できることである。リリース前のrulesを試したければ<code>rules_kotlin_version</code>を個人で更新して試せば良い。他者に影響することなくローカルにインストールできる。

#### BUILD

次に各アプリケーションを見ていく。<code>pkg/public_kt/main.kt</code>はHello Worldを出力する簡単なコードで動いている。

```kotlin
package com.github.soushin.multipojects.publickt

import com.github.soushin.multipojects.commonkt.util.add

fun main(args : Array&lt;String>) {
    println("Hello kotlin!".add())
}

```


Stringクラスの拡張関数である<code>add()</code>をつかっている。この拡張関数は<code>pkg/common_kt/util/string.kt</code>のライブラリを参照している。このようなマルチプロジェクトな仕組みをBazelで実現する方法を整理する。

<hr/>

まずは<code>common_kt/util/string.kt</code>をライブラリとしてビルドする方法をまとめる。

```python
# ./pkg/common_kt/BUILD

package(default_visibility = ["//visibility:public"])

load("@io_bazel_rules_kotlin//kotlin:kotlin.bzl", "kt_jvm_library")

kt_jvm_library(
    name = "common_kt_lib",
    srcs = ["util/string.kt"],
)

```


このBUILDファイルで<code>add()</code>を含むライブラリがビルドできる。<code>kt_jvm_library</code>を有効にして<code>common_kt_lib</code>の名前でビルドしている。このビルドはpublic_ktから参照するのでvisibilityをpublicに定義している。

このcommon_ktをビルドするにはbazelコマンドを実行する。

```
 (bazel-multiprojects) $ bazel build //pkg/common_kt:common_kt_lib
```


上記のコマンドはWORKSPACEが配置されたプロジェクトのルートディレクトリで実行している。<code>//pkg/common_kt</code>がBUILDファイルまでのパスで<code>common_kt_lib</code>がBUILDファイルで定義したビルドタスクである。

<code>bazel-multiprojects/pkg/common_kt</code>のディレクトリで実行する場合は次のコマンドになる。

```
(bazel-multiprojects/pkg/common_kt) bazel build common_kt_lib
```


<hr/>

ライブラリとしてビルドした<code>common_kt_lib</code>を利用する<code>pkg/public_kt/BUILD</code>は次のようになる。

```python
# ./pkg/public_kt/BUILD

package(default_visibility = ["//visibility:public"])

load("@io_bazel_rules_kotlin//kotlin:kotlin.bzl", "kt_jvm_library")

kt_jvm_library(
    name = "public_kt_lib",
    srcs = glob(["main.kt"]),
    deps = ["//pkg/common_kt:common_kt_lib"],
)

java_binary(
    name = "java_bin",
    main_class = "com.github.soushin.multipojects.publickt.MainKt",
    runtime_deps = [":public_kt_lib"],
)

```


<code>kt_jvm_library</code>を使い<code>main.kt</code>を含む<code>public_kt_lib</code>を定義している。public_kt_libでは<code>deps</code>に<code>"//pkg/common_kt:common_kt_lib"</code>が追加 して<code>common_kt</code>を依存させている。

depsの定義によりmain.ktから<code>add()</code>の拡張関数が参照できるようになる。

<code>java_binary</code>はjarを錬成するruleである。main_classには<code>main()</code>関数があるクラスを定義する。<code>runtime_deps</code>ではkt_jvm_libraryでビルドした<code>public_kt_lib</code>を追加する。

これまで複数のruleを定義してきたがHello Worldを出力するビルドは<code>java_binary</code>のruleで定義した<code>java_binary</code>を実行するだけである。java_binaryは<code>public_kt_lib</code>に依存しているのでBazelが依存関係をよしなに解決して実行してくれる。

java_binを実行した結果が次のようになる。

```
 (bazel-multiprojects) $ bazel run //pkg/public_kt:java_bin
INFO: Analysed target //pkg/public_kt:java_bin (2 packages loaded).
INFO: Found 1 target...
Target //pkg/public_kt:java_bin up-to-date:
  bazel-bin/pkg/public_kt/java_bin.jar
  bazel-bin/pkg/public_kt/java_bin
INFO: Elapsed time: 8.150s, Critical Path: 7.60s
INFO: 2 processes: 2 darwin-sandbox.
INFO: Build completed successfully, 6 total actions
INFO: Build completed successfully, 6 total actions
Hello kotlin! - built by Bazel!
```


錬成したjarを実行すると失敗する。

```
 (bazel-multiprojects) $ java -jar bazel-bin/pkg/public_kt/java_bin.jar
 bazel-bin/pkg/public_kt/java_bin.jarにメイン・マニフェスト属性がありません
```


これは単体で動かないjarのため単体で動かすには<code>_deploy.jar</code>をタスクに追加する。

```
 (bazel-multiprojects) $ bazel build //pkg/public_kt:java_bin_deploy.jar
 INFO: Analysed target //pkg/public_kt:java_bin_deploy.jar (0 packages loaded).
 INFO: Found 1 target...
 Target //pkg/public_kt:java_bin_deploy.jar up-to-date:
   bazel-bin/pkg/public_kt/java_bin_deploy.jar
 INFO: Elapsed time: 0.359s, Critical Path: 0.13s
 INFO: 1 process: 1 darwin-sandbox.
 INFO: Build completed successfully, 2 total actions
```


このjarを実行すると正常にHello Worldが出力したjarが起動する。

```
 (bazel-multiprojects) $ java -jar bazel-bin/pkg/public_kt/java_bin_deploy.jar
Hello kotlin! - built by Bazel!
```


<code>name_deploy.jar</code>の仕組みは次のDocsにまとまっているので参照してほしい。

<a href="https://docs.bazel.build/versions/master/be/java.html#java_binary_implicit_outputs">Java Rules#Implicit output targets</a>

### Go

ここまでjarをBazelで錬成できるところまでまとめた。Dockerイメージのビルドは最後にまとめるのでGoプロジェクトを整理する。

#### WORKSPACE

```python
# ./WORKSPACE

load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_archive(
    name = "io_bazel_rules_go",
    url = "https://github.com/bazelbuild/rules_go/releases/download/0.16.2/rules_go-0.16.2.tar.gz",
    sha256 = "f87fa87475ea107b3c69196f39c82b7bbf58fe27c62a338684c20ca17d1d8613",
)

http_archive(
    name = "bazel_gazelle",
    urls = ["https://github.com/bazelbuild/bazel-gazelle/releases/download/0.15.0/bazel-gazelle-0.15.0.tar.gz"],
    sha256 = "6e875ab4b6bf64a38c352887760f21203ab054676d9c1b274963907e0768740d",
)

load("@io_bazel_rules_go//go:def.bzl", "go_rules_dependencies", "go_register_toolchains")

go_rules_dependencies()

go_register_toolchains()

load("@bazel_gazelle//:deps.bzl", "gazelle_dependencies")

gazelle_dependencies()

```


<code>http_archive</code>で必要なruleをインストールしているがGoでは<code>gazelle</code>というGoプロジェクトに特化したBUILDファイルジェネレーターが用意されている。


<div class="github-card" data-user="bazelbuild" data-repo="bazel-gazelle" data-width="400" data-height="" data-theme="default"></div>
<script src="https://cdn.jsdelivr.net/github-cards/latest/widget.js"></script>


#### Gazelleの威力

KotlinではHello Worldを出力するアプリケーションとライブラリの依存関係をBUILDファイルに定義したがgazelleはプロジェクト内のgoファイルを解析してBUILDファイルを錬成してくれる！

<code>WORKSPACE</code>と同階層に次のようなBUILDファイルを配置する。

```python
# ./BUILD

load("@bazel_gazelle//:def.bzl", "gazelle")

# gazelle:prefix github.com/soushin/bazel-multiprojects
gazelle(name = "gazelle")

```


<code>gazelle</code>を有効にしている箇所で<code>gazelle:prefix</code>でgoのimportパスのprefixを定義している。このBUILDファイルを配置した状態で次のコマンドを実行する。

```
 (bazel-multiprojects) $ bazel run gazelle
```


上記のコマンドを実行すると自動で<code>public_go</code>と<code>common_go</code>のgoコードを解析してBUILDファイルが錬成される。錬成したBUILDファイルを見ていく。

まずはcommon_goを見ていく。

```python
# ./pkg/common_go/BUILD.bazel ※ gazelleが錬成したファイルは`.bazel`が含まれる。

load("@io_bazel_rules_go//go:def.bzl", "go_library")

go_library(
    name = "go_default_library",
    srcs = ["string.go"],
    importpath = "github.com/soushin/bazel-multiprojects/pkg/common_go/util",
    visibility = ["//visibility:public"],
)

```


<code>go_library</code>のruleが定義されている。importpathは<code>gazelle:prefix</code>で定義したプレフィックスが適応されている。

このgo_default_libraryをpublic_ktに依存させていく。

<hr/>

次にpublic_goのBUILDファイルは次のようになっている。

```python
# ./pkg/public_go/BUILD.bazel

load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library")

go_library(
    name = "go_default_library",
    srcs = ["main.go"],
    importpath = "github.com/soushin/bazel-multiprojects/pkg/public_go",
    visibility = ["//visibility:private"],
)

go_binary(
    name = "public_go",
    embed = [":go_default_library"],
    visibility = ["//visibility:public"],
)

```


main.goは<code>main</code>パッケージに配置しているので<code>go_binary</code>のruleが追加になっている。このgo_binaryを実行してみると次のようになる。

```
 (bazel-multiprojects) $ bazel run //pkg/public_go:public_go    
INFO: Analysed target //pkg/public_go:public_go (0 packages loaded).
INFO: Found 1 target...
Target //pkg/public_go:public_go up-to-date:
  bazel-bin/pkg/public_go/darwin_amd64_stripped/public_go
INFO: Elapsed time: 0.571s, Critical Path: 0.37s
INFO: 2 processes: 2 darwin-sandbox.
INFO: Build completed successfully, 3 total actions
INFO: Build completed successfully, 3 total actions
Hello World!
```


<code>Hello World!</code>が出力できた。次にcommon_goを依存させて<code>Hello kotlin! - built by Bazel!</code>と出力するようにしてみる。

<hr/>

錬成したライブラリを依存させるにはKotlinと同様にdepsを用いる。

```python
go_library(
    name = "go_default_library",
    srcs = ["main.go"],
    importpath = "github.com/soushin/bazel-multiprojects/pkg/public_go",
    visibility = ["//visibility:private"],
    deps = ["//pkg/common_go/util:go_default_library"],
)

```


depsに<code>go_default_library</code>のライブラリを追加した。この状態で<code>go_default_library</code>をビルドするとmain.goからcommon_go/util/stirng.goが参照できるようになる。

```go
package main

import (
    "fmt"

    "github.com/soushin/bazel-multiprojects/pkg/common_go/util"
)

func main() {
    fmt.Println(util.Add("Hello World!"))
}

```


最後にgo_binaryを実行する。

```
 (bazel-multiprojects) $ bazel run //pkg/public_go:public_go 
INFO: Analysed target //pkg/public_go:public_go (0 packages loaded).
INFO: Found 1 target...
Target //pkg/public_go:public_go up-to-date:
  bazel-bin/pkg/public_go/darwin_amd64_stripped/public_go
INFO: Elapsed time: 0.629s, Critical Path: 0.41s
INFO: 2 processes: 2 darwin-sandbox.
INFO: Build completed successfully, 3 total actions
INFO: Build completed successfully, 3 total actions
Hello World! - built by Bazel!
```


無事、<code>util/stirng.go</code>がmain.goに依存できた。

### Dockerイメージのビルド

KotlinとGoのDockerイメージのビルドを見ていく。今回はビルドしたイメージをローカルにインストールするところまでをまとめていく。

#### Kotlin

WORKSPACEに次のhttp_archiveを追加する。

```python
# ./WORKSPACE

load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_archive(
    name = "io_bazel_rules_docker",
    sha256 = "29d109605e0d6f9c892584f07275b8c9260803bf0c6fcb7de2623b2bedc910bd",
    strip_prefix = "rules_docker-0.5.1",
    urls = ["https://github.com/bazelbuild/rules_docker/archive/v0.5.1.tar.gz"],
)

```


Dockerをビルドするためのruleをインストールする定義である。

Kotlinプロジェクトで錬成したjarを動かすJavaベースのDockerイメージをプロジェクト内にインストールしたい。そのような場合は次のようにWORKSPACEに追加する。

```python
# ./WORKSPACE

load("@io_bazel_rules_docker//container:container.bzl", "container_pull")

container_pull(
    name = "java_base_image",
    registry = "index.docker.io",
    repository = "library/openjdk",
    tag = "12-ea-18-jdk-alpine3.8",
)

```


<code>container_pull</code>のruleを有効にしてdocker.ioにあるalpineベースのjdkイメージを<code>java_baze_image</code>としてプロジェクトにインストールした。

ここまでがWORKSPACEに追加するKotlin向けのプロジェクト全般の定義である。

##### BUILD

<code>container_pull</code>したjava_base_imageをベースにHello Worldを出力するアプリケーションを内包したDockerイメージを錬成していく。

次のようなcontainer_imageのruleを有効にして定義する。

```python
# ./pkg/public_kt/BUILD

load("@io_bazel_rules_docker//container:container.bzl", "container_image")

container_image(
    name = "public_kt_image",
    base = "@java_base_image//image",
    files = [":java_bin_deploy.jar"],
    cmd = [
        "java",
        "-jar",
        "java_bin_deploy.jar",
    ],
)

```


<code>base</code>はWORKSPACEに追加した<code>java_base_image</code>を設定している。filesには<code>java_bin</code>のタスクで錬成するjarファイルを設定しDockerイメージ内に配置されるようにしている。あとはイメージ内に配置したjarを実行するcmdを設定している。

このタスクを実行すると次のようになる。

```
 (bazel-multiprojects) $ bazel run //pkg/public_kt:public_kt_image
 INFO: Analysed target //pkg/public_kt:public_kt_image (14 packages loaded).
 INFO: Found 1 target...
 Target //pkg/public_kt:public_kt_image up-to-date:
   bazel-bin/pkg/public_kt/public_kt_image-layer.tar
 INFO: Elapsed time: 5.201s, Critical Path: 2.85s
 INFO: 4 processes: 4 darwin-sandbox.
 INFO: Build completed successfully, 5 total actions
 INFO: Build completed successfully, 5 total actions
 84c6c9b35317: Loading layer [==================================================>]  972.8kB/972.8kB
 Loaded image ID: sha256:a5cf4f2786c475fe57d020a86d74f2b686a85701908e9c9128c24219b078e809
 Tagging a5cf4f2786c475fe57d020a86d74f2b686a85701908e9c9128c24219b078e809 as bazel/pkg/public_kt:public_kt_image
```


ローカルに<code>bazel/pkg/public_kt:public_kt_image</code>の名前とタグでイメージがビルドできた。このDockerイメージを起動する。

```
 (bazel-multiprojects) $ docker run -it bazel/pkg/public_kt:public_kt_image
 Hello kotlin! - built by Bazel!
```


Bazelで錬成したjarをBazelによって錬成したDockerイメージに内包することができた。

#### Go

GoのDockerイメージの錬成を見ていく。

WORKSPACEに次のhttp_archiveを追加する。

```python
# ./WORKSPACE

load(
    "@io_bazel_rules_docker//go:image.bzl",
    _go_image_repos = "repositories",
)
_go_image_repos()

```


Kotlinとは異なりBazelが提供するイメージを利用する。

##### BUILD

```python
# ./pkg/public_go/BUILD

load("@io_bazel_rules_docker//go:image.bzl", "go_image")

go_image(
    name = "public_go_base_image",
    embed = [":go_default_library"],
    importpath = "github.com/soushin/bazel-multiprojects/pkg/public_go",
    goarch = "amd64",
    goos = "linux",
    pure = "on",
)

load("@io_bazel_rules_docker//container:container.bzl", "container_image")

container_image(
    name = "public_go_image",
    base = ":public_go_base_image",
)

```


<code>go_image</code>のruleを有効にしてGoのDockerイメージを錬成している。embedにはライブラリの<code>go_default_library</code>を設定している。この<code>public_go_base_image</code>をcontainer_imageのbaseにしてローカルにDockerイメージがインストールする。

<code>go_image</code>のpublic_go_base_imageだけでもDockerイメージはインストールできるのだがcontainer_imageはportやenvなどの細かいビルド設定が定義できる。今回のエンドリでは利用用途がなく割愛しているがHTTP Serverのようなアプリケーションではもう少し設定が増えると思う。

<code>public_go_image</code>のタスクを実行すると次のようになる。

```
 (bazel-multiprojects) $ bazel run //pkg/public_go:public_go_image
INFO: Analysed target //pkg/public_go:public_go_image (0 packages loaded).
INFO: Found 1 target...
Target //pkg/public_go:public_go_image up-to-date:
  bazel-bin/pkg/public_go/public_go_image-layer.tar
INFO: Elapsed time: 1.137s, Critical Path: 0.49s
INFO: 2 processes: 2 darwin-sandbox.
INFO: Build completed successfully, 3 total actions
INFO: Build completed successfully, 3 total actions
84ff92691f90: Loading layer [==================================================>]  10.24kB/10.24kB
Loaded image ID: sha256:71560af2643cc9a1f0593ff72940ae99711d5d572156187c930bfdb6aa740bbf
Tagging 71560af2643cc9a1f0593ff72940ae99711d5d572156187c930bfdb6aa740bbf as bazel/pkg/public_go:public_go_image
```


ローカルにDockerイメージがインストールできたのでDockerイメージを起動する。

```
(bazel-multiprojects) $ docker run -it bazel/pkg/public_go:public_go_image
Hello World! - built by Bazel!
```


Kotlinと同様にバイナリの錬成からDockerイメージのビルドまでBazelのみで完遂できた。

### まとめ

最終的なプロジェクトの構造は次のようになった。

```
./
├── BUILD
├── README.md
├── WORKSPACE
├── bazel-bazel-multiprojects -> /path/to/symlink
├── bazel-bin -> /path/to/symlink
├── bazel-genfiles -> /path/to/symlink
├── bazel-out -> /path/to/symlink
├── bazel-testlogs -> /path/to/symlink
└── pkg
    ├── common_go
    │   └── util
    │       ├── BUILD.bazel
    │       └── string.go
    ├── common_kt
    │   ├── BUILD
    │   └── util
    │       └── string.kt
    ├── public_go
    │   ├── BUILD.bazel
    │   └── main.go
    └── public_kt
        ├── BUILD
        └── main.kt
```


※Bazelが出力する<code>bazel-*</code>のシンボリックリンクのパスは<code>/path/to/symlink</code>に置換しています。

プロジェクトのルートにWORKSPACEが配置され各アプリケーションとライブラリにBUILDファイルが配置されているのが分かる。BazelはGoとKotlinの言語が混合したプロジェクト構造であってもビルド環境は１つなので言語環境が競合することなく１つのレポジトリに配置することができる。

GoとKotlinが言語間でやり取りすることはないので１つのレポジトリに配置することはメリットがないかもしれない。あるとすればWORKSPACEの設定は共有されるのでGoとKotlinプロジェクトのビルド環境の差異を防げるくらいでだろうか。このエントリを通して今後プロジェクトに導入して事例を貯めていけるようなフェーズに入れたので引き続きBazelを推進していきたい。

今回では触れられなかったところを備忘録としてリストする。

<ul>
<li>grpcを利用するのでBazelのビルド定義は今のところ分からない。grpc-javaやgoもexampleはあるようなので見ていきたい。</li>
<li>container_imageのビルドまでをまとめたが実際のプロジェクトではcontainer_pushを利用することになる。CI視点の知見も貯めていきたい。</li>
<li>KtorやSpring-fu、Goであれば各種ライブラリが依存するような実戦形式のBUILDファイルの運用知見も引き続きまとめていきたい。</li>
</ul>


### コード

このエントリで紹介したコード一式は次のレポジトリに置いてあるので参照してください。


<div class="github-card" data-user="soushin" data-repo="bazel-multiprojects" data-width="400" data-height="" data-theme="default"></div>
<script src="https://cdn.jsdelivr.net/github-cards/latest/widget.js"></script>



